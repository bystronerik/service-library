plugins {
	id "com.diffplug.spotless" version "5.13.0"
	id 'org.springframework.boot' version '2.5.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'maven-publish'
	id 'java-library'
}

group = 'com.deizon'
version = '2.6.9'
sourceCompatibility = JavaVersion.VERSION_13
targetCompatibility = JavaVersion.VERSION_13

bootJar {
	enabled = false
}

jar {
	enabled = true
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = 'com.deizon.services'
			artifactId = 'ServicesLibrary'
			version = getVersion()

			from components.java
		}
	}
	repositories {
		maven {
			url "https://maven.pkg.jetbrains.space/deizon/p/servicelibrary/maven"
			credentials {
				username=project.properties['username']
				password=project.properties['password']
			}
		}
	}
}

dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.20'
	annotationProcessor 'org.projectlombok:lombok:1.18.20'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:2.5.4'
	implementation 'org.springframework.boot:spring-boot-starter:2.5.4'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb:2.5.4'
	implementation 'org.springframework.boot:spring-boot-starter-security:2.5.4'
	implementation 'org.springframework.boot:spring-boot-starter-actuator:2.5.4'
	implementation 'org.springframework.boot:spring-boot-starter-webflux:2.5.4'
	implementation 'org.springframework:spring-aspects:5.3.9'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
	implementation 'com.graphql-java-kickstart:graphql-spring-boot-starter:11.0.0'
	implementation 'com.graphql-java-kickstart:graphql-java-tools:11.0.0'
	implementation 'com.graphql-java-kickstart:graphql-java-servlet:11.0.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2', 'io.jsonwebtoken:jjwt-jackson:0.11.2'
	testCompileOnly 'org.projectlombok:lombok:1.18.20'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
	testImplementation 'org.springframework.security:spring-security-test:5.5.1'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.4'
	testImplementation 'com.graphql-java-kickstart:graphql-spring-boot-starter-test:11.0.0'
}

test {
	useJUnitPlatform()
}

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		importOrder()
		removeUnusedImports()
		googleJavaFormat().aosp()
	}
}
